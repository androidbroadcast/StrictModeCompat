/*
 * Copyright 2017-2019 Kirill Rozov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group = 'com.kirich1409'
version = rootProject.ext.strictModeCompatVersionName

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/kirich1409/StrictModeCompat'
def gitRepoUrl = 'https://github.com/kirich1409/StrictModeCompat.git'

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        publications {
            releaseAar(MavenPublication) {
                from components.release
                artifact(sourceJar)

                groupId = 'com.kirich1409'
                artifactId = 'strict-mode-compat'
                version = rootProject.ext.versionName

                pom {
                    name = 'Strict Mode Compat'
                    description = 'Android Strict Mode Compat Library'
                    url = siteUrl
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = 'kirich1409'
                            name = 'Kirill Rozov'
                            email = 'krl.rozov@gmail.com'
                        }
                    }
                    scm {
                        url = gitRepoUrl
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('apiKey') ? project.property('apiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ["releaseAar"]

    pkg {
        repo = 'maven'
        name = 'strict-mode-compat'
        desc = 'Android Strict Mode Compat Library'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/kirich1409/StrictModeCompat/issues'
        vcsUrl = gitRepoUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'strict mode', 'compat']
//        publish true
        publicDownloadNumbers = true

        githubRepo = 'kirich1409/StrictModeCompat' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            name = rootProject.ext.strictModeCompatVersionName
            desc = 'Android StrictMode Compat Library'
            released = new Date()
        }
    }
}

