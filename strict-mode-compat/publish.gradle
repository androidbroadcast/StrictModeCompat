group = 'com.kirich1409'
version = rootProject.ext.versionName

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/kirich1409/StrictModeCompat'
def gitRepoUrl = 'https://github.com/kirich1409/StrictModeCompat.git'

def sourcesJar = tasks.register("sourcesJar", Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def javadoc = tasks.register("javadoc", Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

def javadocJar = tasks.register("javadocJar", Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.get().destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('apiKey') ? project.property('apiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'strict-mode-compat'
        desc = 'Android Strict Mode Compat Library'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/kirich1409/StrictModeCompat/issues'
        vcsUrl = gitRepoUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'strict mode', 'compat']
//        publish true
        publicDownloadNumbers = true

        githubRepo = 'kirich1409/StrictModeCompat' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            name = rootProject.ext.versionName
            desc = 'Android StrictMode Compat Library'
            released = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Android Strict Mode Compat Library'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kirich1409'
                        name 'Kirill Rozov'
                        email 'krl.rozov@gmail.com'
                    }
                }
                scm {
                    connection gitRepoUrl
                    developerConnection gitRepoUrl
                    url siteUrl
                }
            }
        }
    }
}
